module Main exposing (main)

import Browser
import Browser.Events
import Element exposing (..)
import Element.Background as Background
import Element.Border as Border
import Element.Font as Font
import Element.Input as Input
import Html
import Html.Attributes



-- üí• ü™ê üëΩ ‚ñ∂Ô∏è ‚è∏Ô∏è üõ∏


type State
    = Playing
    | Over
    | Paused


type Direction
    = Left
    | Right


type alias Model =
    { points : Int
    , positionX : Float
    , positionY : Float
    , direction : Direction
    , state : State
    , aliens : List ( Float, Float )
    }


init : ( Model, Cmd msg )
init =
    ( { points = 30
      , positionX = 0
      , positionY = 0
      , direction = Right
      , state = Playing
      , aliens = []
      }
    , Cmd.none
    )


type Msg
    = Increment
    | Fire
    | OnAnimationFrame Float
    | TogglePause


speedX : Float
speedX =
    5


speedY : Float
speedY =
    2


speedAlien : Float
speedAlien =
    4


update : Msg -> Model -> ( Model, Cmd msg )
update msg model =
    case msg of
        Increment ->
            ( { model | points = model.points + 1 }, Cmd.none )

        Fire ->
            ( { model
                | aliens = ( model.positionX, model.positionY ) :: model.aliens
                , points = model.points - 1
                , state =
                    if model.points <= 1 then
                        Over

                    else
                        model.state
              }
            , Cmd.none
            )

        OnAnimationFrame delta ->
            ( { model
                | positionX =
                    case model.direction of
                        Left ->
                            model.positionX - speedX

                        Right ->
                            model.positionX + speedX
                , positionY =
                    if model.positionX > 270 || model.positionX < 0 then
                        model.positionY + speedY

                    else
                        model.positionY
                , direction =
                    if model.positionX > 270 then
                        Left

                    else if model.positionX < 0 then
                        Right

                    else
                        model.direction
                , aliens =
                    model.aliens
                        |> List.map
                            (\( alienX, alienY ) -> ( alienX, alienY + speedAlien ))
                        |> List.filter
                            (\( _, alienY ) -> alienY < 500)
                        |> List.filter
                            (\( alienX, alienY ) -> not (hitTargetEnd ( alienX, alienY )))
              }
            , Cmd.none
            )

        TogglePause ->
            ( { model
                | state =
                    case model.state of
                        Playing ->
                            Paused

                        Over ->
                            Over

                        Paused ->
                            Playing
              }
            , Cmd.none
            )


hitTargetStart : ( Float, Float ) -> Bool
hitTargetStart ( alienX, alienY ) =
    alienY > 320 && within { min = 100, max = 200 } ( alienX, alienY )


hitTargetEnd : ( Float, Float ) -> Bool
hitTargetEnd ( alienX, alienY ) =
    alienY > 370 && within { min = 100, max = 200 } ( alienX, alienY )


within : { min : Float, max : Float } -> ( Float, Float ) -> Bool
within { min, max } ( alienX, _ ) =
    min < alienX && alienX < max


attrsButton : List (Attribute msg)
attrsButton =
    [ Border.width 1
    , paddingXY 20 10
    , Border.rounded 100
    , Background.color <| rgb 1 1 1
    , Font.size 40
    ]


view : Model -> Html.Html Msg
view model =
    layout [ padding 20, Background.color <| rgb 0 0.4 0.6 ] <|
        column
            ([ spacing 20
             ]
                ++ [ inFront <|
                        el
                            [ Font.size 150
                            , moveDown 450
                            , moveRight 100
                            ]
                        <|
                            text "ü™ê"
                   ]
                ++ List.map
                    (\( alienX, alienY ) ->
                        inFront <|
                            el
                                [ Font.size 30
                                , moveRight (alienX + 30)
                                , moveDown (alienY + 120)
                                ]
                            <|
                                text
                                    (if hitTargetStart ( alienX, alienY ) then
                                        "üí•"

                                     else
                                        "üëΩ"
                                    )
                    )
                    model.aliens
                ++ (if model.state == Over then
                        [ inFront <|
                            el
                                (attrsButton
                                    ++ [ moveDown 200
                                       , moveRight 50
                                       ]
                                )
                            <|
                                text "Game Over!"
                        ]

                    else
                        []
                   )
            )
            [ row [ spacing 40 ]
                [ Input.button attrsButton
                    { onPress = Just Fire
                    , label = text "üëΩ"
                    }
                , Input.button attrsButton
                    { onPress = Just TogglePause
                    , label =
                        text <|
                            case model.state of
                                Playing ->
                                    "‚è∏Ô∏è"

                                Over ->
                                    "-"

                                Paused ->
                                    "‚ñ∂Ô∏è"
                    }
                , el attrsButton <| text <| String.fromInt model.points
                ]
            , Input.button
                [ moveRight model.positionX
                , moveDown model.positionY
                , Font.size 100
                , case model.direction of
                    Left ->
                        rotate 0

                    Right ->
                        rotate 0.6
                , htmlAttribute <| Html.Attributes.style "transition" "transform 100ms"
                ]
                { onPress = Just Increment, label = text "üõ∏" }
            ]


main : Program () Model Msg
main =
    Browser.element
        { init = \_ -> init
        , view = view
        , update = update
        , subscriptions =
            \model ->
                case model.state of
                    Playing ->
                        Browser.Events.onAnimationFrameDelta OnAnimationFrame

                    Over ->
                        Sub.none

                    Paused ->
                        Sub.none
        }
